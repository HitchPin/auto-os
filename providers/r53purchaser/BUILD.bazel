load("@aspect_rules_esbuild//esbuild:defs.bzl", "esbuild")
load("@aspect_rules_ts//ts:defs.bzl", "ts_project")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@rules_pkg//:mappings.bzl", "pkg_files", "strip_prefix")
load("@rules_pkg//:pkg.bzl", "pkg_zip")

npm_link_all_packages(name = "node_modules")

SRC_PATTERNS = [
    "package.json",
    "src/**/*.ts",
]

TEST_PATTERNS = [
    "test/**/*.spec.ts",
]

SOURCES = glob(
    include = SRC_PATTERNS,
    allow_empty = True,
    exclude = TEST_PATTERNS,
)

DEPS = [
    ":node_modules/@types/node",
    ":node_modules/@types/aws-lambda",
    ":node_modules/@aws-sdk/client-route-53-domains",
    ":node_modules/typescript",
    ":node_modules/@auto-os/opensearch-schemas",
]

ts_project(
    name = "ts-lib",
    srcs = SOURCES,
    declaration = True,
    declaration_dir = "dist",
    out_dir = "dist",
    root_dir = "src",
    transpiler = "tsc",
    visibility = ["//visibility:public"],
    deps = DEPS,
)

esbuild(
    name = "bundle",
    srcs = SOURCES,
    bazel_sandbox_plugin = True,
    entry_point = "src/index.ts",
    format = "cjs",
    metafile = True,
    minify = True,
    output = "index.js",
    output_dir = False,
    platform = "node",
    target = "node18",
    tsconfig = "tsconfig.json",
    deps = DEPS,
)

pkg_files(
    name = "bundle-files",
    srcs = [
        ":bundle",
    ],
    strip_prefix = strip_prefix.files_only(),
)

pkg_zip(
    name = "zip",
    srcs = [
        ":bundle-files",
    ],
    package_file_name = "package.zip",
    visibility = ["//visibility:public"],
)
